if {[lsearch [namespace children] ::tcltest] < 0} {
    package require tcltest 2
    namespace import ::tcltest::*
}


package require sevenzip
package require vfs::sevenzip

catch {
    if {[info exist env(7ZDLL)]} {
        sevenzip initialize $env(7ZDLL)
    } else {
        sevenzip initialize            
    }
}

if {[info proc readFile] eq ""} {
    proc readFile {filename} {
        set f [open $filename "r"]
        try {
            fconfigure $f -encoding utf-8
            return [read $f]
        } finally {
            close $f
        }
    }
}

if {[info proc writeFile] eq ""} {
    proc writeFile {filename data} {
        set f [open $filename "w"]
        try {
            fconfigure $f -encoding utf-8
            puts -nonewline $f $data
        } finally {
            close $f
        }
    }
}

if {[testConstraint vfsdebug]} {
    puts stderr vfs::debug
    proc vfserror {} {vfs::log $::errorInfo}
    vfs::filesystem internalerror vfserror
    set vfs::debug 1
}

testConstraint have7zip [sevenzip isinitialized]
testConstraint encodingOk [expr {"тест" eq "\u0442\u0435\u0441\u0442"}]

set invalidTimeBugFile [file join [testsDirectory] files Microsoft.VisualStudio.Setup.TestMsi.msi] 
testConstraint invalidTimeBug [file exists $invalidTimeBugFile]

test lib7zipvfs-1.0 {mount params} -body {
    vfs::sevenzip::Mount
} -returnCodes 1 -result {wrong # args: should be "vfs::sevenzip::Mount zipfile local ?arg ...?"}

test lib7zipvfs-1.1 {mount non-existent file} -constraints {have7zip} -cleanup {
} -body {
    vfs::sevenzip::Mount [file join [testsDirectory] files notexistent] mnt
} -returnCodes 1 -result "couldn't read file \"[file join [testsDirectory] files notexistent]\": no such file or directory"

test lib7zipvfs-1.2 {mount unknown file} -constraints {have7zip} -cleanup {
} -body {
    vfs::sevenzip::Mount [file join [testsDirectory] files test.txt] mnt
} -returnCodes 1 -result {not supported}

test lib7zipvfs-1.3 {mount/unmount simple file} -constraints {have7zip} -cleanup {
    unset -nocomplain mnt
} -body {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files test.7z] mnt]
    vfs::sevenzip::Unmount $mnt mnt
} -result {}

foreach x {7z zip rar arj tar} {
    foreach {n i r} {
        1 {} {directory 1 0}
        2 testDIRS {directory 1 0}
        3 testDIRX {unknown 0 0}
        4 testDIRS/test4.txt {file 0 1}
        5 testDIRS/testX.txt {unknown 0 0}  
    } {
        test lib7zipvfs-1.3.5-$x.$n {check archive item exists} -constraints {have7zip} -setup {
            set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.$x] mnt]
            set type "unknown"
        } -cleanup {
            vfs::sevenzip::Unmount $mnt mnt; unset mnt
            unset type
        } -body {
            catch {set type [file type mnt/$i]}
            list $type [file isdir mnt/$i] [file isfile mnt/$i]
        } -result $r
        unset n i r
    } 
}

foreach x {7z zip rar arj tar} {
    test lib7zipvfs-1.3.6-$x {check archive files from dir} -constraints {have7zip} -setup {
        set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.$x] mnt]
    } -cleanup {
        vfs::sevenzip::Unmount $mnt mnt; unset mnt
    } -body {
        list \
                [lsort [glob -nocomplain -tails -dir mnt *]] \
                [lsort [glob -nocomplain -tails -dir mnt/testDIRS *\[23456\]*]] \
                [lsort [glob -nocomplain -tails -dir mnt/testDIRS/test3/test32 *]]
    } -result {testDIRS {test2 test3 test4.txt test5.txt test6.txt} test321.txt}
    unset x
}

test lib7zipvfs-1.4.1 {mount/glob simple file} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files test.7z] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    glob mnt/*
} -result {mnt/test.txt}

test lib7zipvfs-1.4.2 {mount/glob simple file tail} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files test.7z] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    glob -directory mnt -tails *
} -result {test.txt}

test lib7zipvfs-1.5.1 {mount/open simple file} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files test.7z] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    readFile mnt/test.txt
} -result {test}

test lib7zipvfs-1.5.2 {mount/open complex file} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.7z] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    readFile mnt/testDIRS/test4.txt
} -result {test4}

test lib7zipvfs-1.5.3 {mount/open complex zip file} -constraints {have7zip} -setup {
    set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.zip] mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    readFile mnt/testDIRS/test4.txt
} -result {test4}

foreach {n a g r} {
    1 "" "mnt"              "mnt"
    2 "" "mnt/"             "mnt"
    3 "" "mnt/../mnt"       "mnt/../mnt"
    4 "" "mnt/testDIRS"     "mnt/testDIRS"
    5 "" "mnt/*"            "mnt/testDIRS"
    6 "-types d" "mnt/*"    "mnt/testDIRS"
    7 "-types f" "mnt/*"    ""
    8 "" "mnt/testDIRS/test4.txt"       "mnt/testDIRS/test4.txt"
    9 "-dir mnt" ""                     "mnt"
    A "-dir mnt/../mnt" ""              "mnt/../mnt"
    C "-dir mnt/testDIRS" ""            "mnt/testDIRS"
    D "-dir mnt/testDIRS/test4.txt" ""  "mnt/testDIRS/test4.txt"
} {
    test lib7zipvfs-1.6.$n "glob $a '$g'" -constraints {have7zip} -setup {
        set mnt [vfs::sevenzip::Mount [file join [testsDirectory] files testDIRS.zip] mnt]
    } -cleanup {
        vfs::sevenzip::Unmount $mnt mnt; unset mnt
    } -body {
        glob -nocomplain {*}$a $g
    } -result $r

    test lib7zipvfs-1.6zip.$n "zip glob $a $g" -constraints {vfsdebug} -setup {
        package require vfs::zip
        set mnt [vfs::zip::Mount [file join [testsDirectory] files testDIRS.zip] mnt]
    } -cleanup {
        vfs::zip::Unmount $mnt mnt; unset mnt
    } -body {
        glob -nocomplain {*}$a $g
    } -result $r

    unset n g r
}

test lib7zipvfs-2.1.invalidTimeBug {invalidTimeBug} -constraints {have7zip invalidTimeBug} -setup {
    set mnt [vfs::sevenzip::Mount $invalidTimeBugFile mnt]
} -cleanup {
    vfs::sevenzip::Unmount $mnt mnt; unset mnt
} -body {
    file isfile mnt/!_Columns
} -result {1}


cleanupTests
return
